plugins {
    id 'java'
    id 'application'
}

java {
    sourceCompatibility = JavaVersion.VERSION_1_7
    targetCompatibility = JavaVersion.VERSION_1_7
}

repositories {
    google()
    mavenCentral()
    maven { url "https://oss.sonatype.org/content/repositories/snapshots/" }
    maven { url "https://jitpack.io" }
}

application {
    mainClass = 'com.minimine.Iniciador'
}

dependencies {
    implementation project(':gdx-jogo')

    // motor principal e motor de janelas(vers√£o 2)
    implementation "com.badlogicgames.gdx:gdx:1.10.0"
    implementation "com.badlogicgames.gdx:gdx-backend-lwjgl:1.10.0"
    
    implementation "com.badlogicgames.gdx:gdx-freetype:1.10.0"
    implementation "com.github.mgsx-dev.gdx-gltf:gltf:2.1.0"

    // arquivos de sistema
    runtimeOnly "com.badlogicgames.gdx:gdx-platform:1.10.0:natives-desktop"
    runtimeOnly "com.badlogicgames.gdx:gdx-freetype-platform:1.10.0:natives-desktop"
}

sourceSets {
    main {
        java {
            srcDirs = ['src']
        }
        resources {
            srcDirs = ['../assets']
        }
    }
}

tasks.withType(JavaCompile) {
    options.encoding = 'UTF-8'
    options.incremental = false
}

task rodar(type: JavaExec) {
    mainClass = 'com.minimine.Iniciador'
    classpath = sourceSets.main.runtimeClasspath
    standardInput = System.in

    workingDir = file('../assets').exists() ? file('../assets') : projectDir
}

jar {
    archiveFileName = "MiniMine.jar"
    duplicatesStrategy = DuplicatesStrategy.EXCLUDE
    manifest {
        attributes 'Main-Class': 'com.minimine.Iniciador'
    }
    from {
        configurations.runtimeClasspath.collect { it.isDirectory() ? it : zipTree(it) }
    }
    with copySpec {
        from 'assets'
    }
}
