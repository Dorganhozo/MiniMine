plugins {
    id 'java'
    id 'application'
}

repositories {
    google()
    mavenCentral()
    maven { url "https://oss.sonatype.org/content/repositories/snapshots/" }
    maven { url "https://jitpack.io" }
}

application {
    mainClass = 'com.minimine.Iniciador'
}

dependencies {
    implementation project(':gdx-jogo')
    implementation "com.badlogicgames.gdx:gdx-backend-lwjgl:$gdxVersion"
    implementation "com.github.mgsx-dev.gdx-gltf:gltf:2.1.0"
    runtimeOnly "com.badlogicgames.gdx:gdx-platform:$gdxVersion:natives-desktop"
    runtimeOnly "com.badlogicgames.gdx:gdx-freetype-platform:$gdxVersion:natives-desktop"
    runtimeOnly "com.badlogicgames.gdx:gdx-platform:$gdxVersion:natives-x86"
    runtimeOnly "com.badlogicgames.gdx:gdx-freetype-platform:$gdxVersion:natives-x86"
}

sourceSets {
    main {
        java { srcDirs = ['src'] }
        resources { srcDirs = ['../gdx-android/assets'] }
    }
}

tasks.withType(JavaCompile) {
    options.encoding = 'UTF-8'
    options.incremental = false
}

task rodar(type: JavaExec) {
    mainClass = 'com.minimine.Iniciador'
    classpath = sourceSets.main.runtimeClasspath
    standardInput = System.in
    workingDir = file('../gdx-android/assets').exists() ? file('../gdx-android/assets') : projectDir
}

tasks.withType(JavaExec).configureEach{
    doFirst {
	def logsDir = file("${rootDir}/.cache/logs")
	
	if(!logsDir.exists()){
		logsDir.mkdirs()
		println"Pasta ${logsDir} foi criado"
	}
    }

    jvmArgs += "-XX:ErrorFile=${rootDir}/.cache/logs/hs_err_pid%p.log"
}

jar {
    archiveFileName = "minimine.jar"
    duplicatesStrategy = DuplicatesStrategy.EXCLUDE
    manifest {
        attributes 'Main-Class': 'com.minimine.Iniciador'
    }
    from {
        configurations.runtimeClasspath.collect { 
            it.exists() && !it.path.contains("gdx-jogo/build") ? (it.isDirectory() ? it : zipTree(it)) : null 
        }.findAll { it != null }
    }
    from project(':gdx-jogo').sourceSets.main.output
}
