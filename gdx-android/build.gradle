apply plugin: 'com.android.application'

android {
    namespace "com.minimine"
    compileSdkVersion 34

    defaultConfig {
        applicationId "com.minimine"
        minSdkVersion 23
        targetSdkVersion 26
        versionCode 1
        versionName "1.0"
    }
    
    buildTypes {
        release {                 
            minifyEnabled false
            proguardFiles getDefaultProguardFile('proguard-android.txt'), 'proguard-rules.pro'
        }
    }
    
    sourceSets {
        main {
            manifest.srcFile 'AndroidManifest.xml'
            res.srcDirs = ['res']
            java.srcDirs = ['src']
            assets.srcDirs = ['../assets']
            jniLibs.srcDirs = ['libs']
        }
    }

    compileOptions {
        sourceCompatibility = JavaVersion.VERSION_17
        targetCompatibility = JavaVersion.VERSION_17
    }
}

dependencies {   
    implementation project(":gdx-jogo")
    implementation fileTree(dir: 'libs', include: ['*.jar'])
    implementation fileTree(dir: '../gdx-jogo/libs', include: ['*.jar']) 
}

// desabilita cache para forçar recompilação sempre
configurations.all {
    resolutionStrategy.cacheChangingModulesFor 0, 'seconds'
    resolutionStrategy.cacheDynamicVersionsFor 0, 'seconds'
}

// tarefa personalizada pra limpar seletivamente
task limpezaSele(type: Delete) {
    description = 'Limpa apenas classes compiladas, preservando outras compilações'
    delete 'build/classes'
    delete 'build/intermediates/classes'
    delete 'build/tmp'
}
// força recompilação completa sempre que assets ou src mudarem
tasks.whenTaskAdded { task ->
    if(task.name.contains('Compile') || task.name.contains('JavaPreCompile')) {
        task.outputs.upToDateWhen { false }
    }
}
// gancho pra sempre recompilar quando assets mudam
android.applicationVariants.all { variant ->
    variant.mergeAssets.doFirst {
        println "Assets modificados - forçando recompilação completa..."
        file('build/classes').deleteDir()
    }
}

// torna a tarefa limpezaSele disponivel antes da recompilação
tasks.clean.dependsOn limpezaSele
